apply plugin: 'maven-publish'
apply plugin: 'java'

publishing {
    publications {
        release(MavenPublication) {
            // The coordinates of the library, being set from variables that
            // we'll set up in a moment
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION

            artifact jar

            // Self-explanatory metadata for the most part
            pom {
                name = PUBLISH_ARTIFACT_ID
                description = 'A gradle plugin that merge dependencies into the final aar file works with AGP 3.+'
                
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // patch the dependency section of the pom file
                    project.configurations.implementation.allDependencies.each {
                        if (it.group == "com.android.tools.build" && it.name == "gradle") {
                            return
                        }
                        if (it.group == null) {
                            return
                        }
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/Legion2/fat-aar-android")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
